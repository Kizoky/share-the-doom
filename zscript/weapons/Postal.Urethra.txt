class Piss : Ammo
{
	Default
	{
		Inventory.MaxAmount 20;
		+Inventory.UNDROPPABLE;
		-Inventory.INVBAR;
	}
}

class PostUrethra : PostalWeapon
{
	bool Sigh, PlaySoundOnce;
	
	override void Tick()
	{
		Super.Tick();
		
		if (!Owner) return;
	
	}
	
	Default
	{
		Weapon.Kickback 100;
		Weapon.SelectionOrder 1300;
		Weapon.AmmoUse 0;
		Weapon.AmmoType "Piss";
		Obituary "$PSTL_OB_URETHRA";
		Tag "$PSTL_WP_URETHRA";
		Inventory.PickupMessage "$PSTL_PP_URETHRA";
		+Weapon.NoAlert;
		+INVENTORY.UNDROPPABLE;
		+INVENTORY.UNTOSSABLE;
		PostalWeapon.STD_Melee true;
	}
	States
	{
	Ready:
		URET A 1 A_WeaponReady;
		Goto Ready;
	Deselect:
		URET A 0
		{
			if (!invoker.PlaySoundOnce)
			{
				A_StartSound("piss/zipperup", CHAN_BODY);
				invoker.PlaySoundOnce = true;
			}
		}
		URET A 1 A_Lower;
		Loop;
	Select:
		URET A 0
		{
			invoker.PlaySoundOnce = false;
		}
		URET A 1 A_Raise;
		Loop;
	Fire:
		URET A 0
		{
			if (!invoker.Sigh) invoker.Sigh = true;
			A_FireProjectile("Urine");
		}
		URET A 1 A_StartSound("piss/loop", CHAN_WEAPON);
		Goto FireHold;
	FireHold:
		URET A 1
		{	
			int i = 0;
			while (i < 3)
			{
				actor piss = A_FireProjectile("Urine",0,0,0,frandom(-14,-15));
				i++;
			}
			
			if (!(player.cmd.buttons & BT_ATTACK))
			{
				return ResolveState ("Ready");
			}
			return ResolveState (null);
		}
		Loop;
	Spawn:
		SHOT A -1;
		Stop;
	}
}

class Urine : Actor
{
	bool d;
	override void PostBeginPlay()
	{
		Super.PostBeginPlay();
		Scale.X = frandom(0.07,0.20);
		Scale.Y = frandom(0.05,1);
		bSPRITEFLIP = random(0,1);
		
		int r = random(1,2);
		d = random(0,1);
		
		bool e = random(0,1);
		if (e)
			roll = 180;
		else
			roll = -180;
		
		switch(r)
		{
			case 1:
				SetStateLabel("One");
				break;
			case 2:
				SetStateLabel("Two");
				break;
			default:
				break;
		}
	}
	
	override void Tick()
	{
		Super.Tick();
		
		if (d)
			roll += 0.5;
		else
			roll -= 0.5;
		
		
		vel.z -= 0.2;
	}
	
	Default
	{
		Species "Swordfight";
		Radius 5;
		Height 5;
		Speed 10;
		Damage 0;
		Projectile;
		+THRUSPECIES
		-NOBLOCKMAP
		-NOGRAVITY
		+SHOOTABLE
		+NOBLOOD
		Gravity 0.27;
		Scale 0.07;
		BloodType "null";
		RenderStyle "Translucent";
		Alpha 0.21;
		+ROLLSPRITE
		+FORCEXYBILLBOARD
		//SeeSound "";
		DeathSound "";
		//BounceSound "";
		Obituary "%o got pissed on by %k ... ???";
	}
	States
	{
		Spawn:
			TNT1 A 0;
			Loop;
		One:
			URIN A 3;
			Loop;
		Two:
			URIN B 3;
			Loop;
		Death:
			TNT1 A 0
			{
				if (CheckProximity("UrinePissPool", 1, 1, CPXF_SETMASTER | CPXF_CHECKSIGHT | CPXF_CLOSEST))
				{
					let a = UrinePissPool(master);
					if (a)
					{
						a.Scale += (0.001,0.001);
						a.A_SetSize(a.Scale.X*14/1);
					}
				}
				else
					Spawn("UrinePissPool", pos);
				
				if (d) return;
				
				int i = 0;
				while (i < 1)
				{
					vector3 poss = (frandom(-0.1,0.1),frandom(-0.1,0.1),frandom(0.1,0.3));
					let Splash = Spawn("UrineSplash", pos+poss);
					if (Splash)
					{	
						Splash.vel = (frandom(-1.0,1.0),frandom(-1.5,1.5),frandom(-3.5,3.5));
					}
					i++;
				}
				
			}
			Stop;
	}
}

class UrineSplash : Actor
{
	bool d;
	int TimeDestruct;
	override void PostBeginPlay()
	{
		Super.PostBeginPlay();
		bSPRITEFLIP = random(0,1);
		
		int r = random(1,2);
		d = random(0,1);
		
		Scale.Y = frandom(0.05,0.15);
		Scale.X = frandom(0.25,0.45);
		
		bool e = random(0,1);
		if (e)
			roll = 180;
		else
			roll = -180;
		
		TimeDestruct = level.time + 8;
		
		switch(r)
		{
			case 1:
				SetStateLabel("One");
				break;
			case 2:
				SetStateLabel("Two");
				break;
			default:
				break;
		}
		
		
	}
	
	override void Tick()
	{
		Super.Tick();
		
		if (d)
			roll += 0.5;
		else
			roll -= 0.5;
		
		if (Scale.X <= 0.85)
		{
			Scale.X += 0.01;
			Scale.Y += 0.01;
		}
		
		
		//vel.z += 0.3;
		
		if (TimeDestruct < level.time)
		{
			Alpha -= 0.1;
			if (Alpha <= 0)
				Destroy();
		
		}
		
	}
	
	Default
	{
		Species "Swordfight";
		Radius 3;
		Height 3;
		Speed 10;
		Damage 0;
		Projectile;
		+THRUSPECIES
		-NOBLOCKMAP
		-NOGRAVITY
		+SHOOTABLE
		+NOBLOOD
		Gravity 0.27;
		Scale 0.75;
		BloodType "null";
		RenderStyle "Translucent";
		Alpha 0.50;
		+ROLLSPRITE
		+FORCEXYBILLBOARD
		//SeeSound "";
		DeathSound "";
		//BounceSound "";
	}
	States
	{
		Spawn:
			TNT1 A 0;
			Loop;
		One:
			URIN C 3;
			Loop;
		Two:
			URIN D 3;
			Loop;
		Death:
			TNT1 A 0
			{
				Destroy();
			}
			Stop;
	}
}

class UrinePissPool : Actor
{
	int DestructionTime, Threshold;
	
	override void PostBeginPlay()
	{
		Super.PostBeginPlay();
		DestructionTime = level.time + 35 * 10;
	}
	
	override void Tick()
	{
		Super.Tick();
		if (level.time > DestructionTime)
			Destroy();
	}
	
	Default
	{
		Mass 5;
		Radius 5;
		Height 5;
		-NOBLOCKMAP;
		+NOTELEPORT;
		+ALLOWPARTICLES;
		-NOINTERACTION
		ActiveSound "";
		SeeSound "";
		AttackSound "";
		RenderStyle "Translucent";
		Alpha 0.45;
		Scale 0.32;
		+ROLLSPRITE
	}
	States
	{
	Spawn:
		URIN E 5;
		Loop;
		
	}
}