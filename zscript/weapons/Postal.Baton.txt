class PostBaton : PostalWeapon
{
	action void A_BatonTryHit(string PriOrSec)
	{
		FLineTraceData lineData;
		LineTrace(angle,52,pitch,offsetz: height, data: LineData);
		
		if (LineData.HitType == TRACE_HitNone && PriOrSec ~== "primary")
			A_PlaySound("weapons/baton_pf", CHAN_WEAPON);
		
		if (LineData.HitType == TRACE_HitNone && PriOrSec ~== "secondary")
			A_PlaySound("weapons/baton_sf", CHAN_WEAPON);
		
		if (LineData.HitType == TRACE_HitActor)
		{
			if (!lineData.hitActor) return;
			
			if (lineData.HitActor is 'NPCCore')
			{
				FLineTraceData TraceData;
				LineTrace(invoker.owner.angle,125.0,invoker.owner.pitch,0,Player.ViewHeight-18,0,0,TraceData);
				
				// Headshot
				if (TraceData.HitLocation.Z > pos.z + height-16)
				{
					A_PlaySound("weapons/baton_hit", CHAN_WEAPON);
					A_BatonHit(5);
					return;
				}
				
				A_PlaySound("weapons/baton_hit", CHAN_WEAPON);
				A_BatonHit(0);
				return;
			}
			
			// Normal actor got hit
			A_PlaySound("weapons/baton_hit", CHAN_WEAPON);
			A_BatonHit(0);
		}
		
		if (LineData.HitType == TRACE_HitWall || 
			LineData.HitType == TRACE_HitFloor || 
			LineData.HitType == TRACE_HitCeiling)
		{
			A_PlaySound("weapons/baton_hit", CHAN_WEAPON);
			A_BatonHit(0);
		}
	}
	
	
	action void A_BatonHit(int xtra)
	{
		FTranslatedLineTarget t;

		int damage = xtra + random[batonhit](4, 7);

		double ang = angle;
		double pitch = AimLineAttack (ang, DEFMELEERANGE, null, 0., ALF_CHECK3D);

		LineAttack (ang, DEFMELEERANGE, pitch, damage, 'Melee', "PostalMeleeHitPuff", LAF_ISMELEEATTACK, t);
		
	}
	
	Default
	{
		//$Category STD Weapons
		//$Title "Police Baton"
		//$Sprite PICKA0
		Weapon.Kickback 100;
		Weapon.SelectionOrder 1300;
		Obituary "$PSTL_OB_BATON";
		Tag "$PSTL_WP_BATON";
		Inventory.PickupMessage "$PSTL_PP_BATON";
		+Weapon.NoAlert;
		PostalWeapon.STD_Melee true;
	}
	States
	{
	Ready:
		BATI ABCDEFGHIJKLMNOPQRSTU 7 A_WeaponReady;
		Loop;
	Deselect:
		BATD ABCDEFGHIJKLM 2;
		BATD M 0 A_PostalLower();
		Loop;
	Select:
		BATS A 0 A_PostalRaise(0);
		TNT1 A 0 A_PlaySound("weaponselect1", 7 | 4096);
		BATS ABCDEFGHIJKLMNO 2 A_WeaponReady;
		BATI A 0 A_PostalRaise(1);
		Loop;
	Fire:
		BATF ABCDE 1;
		BATF F 0 A_BatonTryHit("primary");
		BATF FGHIJKLMNOPQRSTUVWXYZ 1;
		Goto Ready;
	AltFire:
		BASF ABC 1;
		BASF D 0 A_BatonTryHit("secondary");
		BASF DEFGHIJKLMNOPQRST 1;
		Goto Ready;
	Spawn:
		TNT1 A 0;
		PICK A -1;
		Stop;
	}
}