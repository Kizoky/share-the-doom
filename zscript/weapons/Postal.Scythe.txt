class PostScythe : PostalWeapon
{
	action void A_ScytheTryHit()
	{
		FLineTraceData lineData;
		LineTrace(angle,52,pitch,offsetz: height, data: LineData);
		
		if (LineData.HitType == TRACE_HitNone)
		{
			A_PlaySound("weapons/scythe_swingmiss", CHAN_WEAPON);
			return;
		}
		
		if (LineData.HitType == TRACE_HitActor)
		{
			if (lineData.hitActor == null) return;
			
			if (LineData.HitActor is 'NPCHead')
			{
				A_PlaySound("weapons/scythe_slice", CHAN_WEAPON);
				A_ScytheHit(50);
				return;
			}
			
			A_PlaySound("weapons/scythe_slice", CHAN_WEAPON);
			A_ScytheHit(0);
			return;
		}
		
		// Todo bodyparts attack (i.e. limbattack)
		
		if (LineData.HitType == TRACE_HitWall)
		{
			A_PlaySound("weapons/scythe_hitwall", CHAN_WEAPON);
			A_ScytheHit(0);
			return;
		}
		
		if (LineData.HitType == TRACE_HitFloor || LineData.HitType == TRACE_HitCeiling)
		{
			A_PlaySound("weapons/scythe_hitwall", CHAN_WEAPON);
			A_ScytheHit(0);
			return;
		}
	}
	
	action void A_ScytheHit(int extradamage)
	{
		FTranslatedLineTarget t;

		int damage = extradamage + random[scythe](14, 18);

		double ang = angle;
		double pitch = AimLineAttack (ang, DEFMELEERANGE, null, 0., ALF_CHECK3D);

		LineAttack (ang, DEFMELEERANGE, pitch, damage, 'Melee', "PostalMeleeHitPuff", LAF_ISMELEEATTACK, t);
	
	}
	
	bool HitGroundOnce;
	
	override void Tick()
	{
		Super.Tick();
		
		if (owner) return;
		
		if (pos.z == floorz && HitGroundOnce != true)
		{
			A_PlaySound("weapons/scythe_hitground", CHAN_WEAPON);
			HitGroundOnce = true;
		}
	}
	
	Default
	{
		//$Category STD Weapons
		//$Title "Scythe"
		//$Sprite PICKA0
		Weapon.Kickback 100;
		Weapon.SelectionOrder 1300;
		Obituary "$PSTL_OB_SCYTHE";
		Tag "$PSTL_WP_SCYTHE";
		Inventory.PickupMessage "$PSTL_PP_SCYTHE";
		+Weapon.NoAlert;
	}
	States
	{
	Ready:
		SCYI ABCDEFGHIJKLMNOP 5 A_WeaponReady;
		Loop;
	Unused:
		SCYH A 0;
		Loop;
	Deselect:
		SCYS EDCBA 4;
		SCYS A 0 A_PostalLower();
		Loop;
	Select:
		SCYS A 0 A_PostalRaise(0);
		SCYS A 0 A_PlaySound("weaponselect1", 7 | 4096);
		SCYS ABCDE 4 A_WeaponReady;
		SCYI A 0 A_PostalRaise(1);
		Loop;
	Fire:
		SWIN ABC 3;
		Goto PullLoop;
	PullLoop:
		SWIN D 3
		{
			if (!(player.cmd.buttons & BT_ATTACK))
				return ResolveState ("SwingFinish");
				
			return ResolveState (null);
		}
		Loop;
	SwingFinish:
		SHOO A 0 A_ScytheTryHit();
		SHOO ABCDEF 4;
		Goto Ready;
	AltFire:
		PEPT ABC 3;
		Goto AltPull;
	AltPull:
		PEPT C 3
		{
			if (!(player.cmd.buttons & BT_ALTATTACK))
				return ResolveState ("AltFinish");
				
			return ResolveState (null);
		
		}
		Loop;
	AltFinish:
		SHO2 A 0
		{
			A_PlaySound("weapons/scythe_throw", CHAN_WEAPON);
			
			class<actor> Scythe = "ThrownScythe";
			Actor ScytheShoot = SpawnPlayerMissile(Scythe);
		
		}
		SHO2 ABCD 4;
		TNT1 A 0
		{
			let p = PostalDude(self);
			if (p && p.PlayerData)
			{
				p.Player.PendingWeapon = p.PlayerData.HolsterWeapon;
				p.Player.ReadyWeapon = p.PlayerData.HolsterWeapon;
			}
			
			let d = Weapon(FindInventory("PostScythe"));
			if (d)
				RemoveInventory(d);
			
			return;
		}
		Goto Ready; // <----- remove weapon from the Player
	UnusedStates:
		// Throw Idle
		TIDL AB 2;
		
		// HolsterEmpty
		HEMP ABCD 2;
		
		Goto Ready;
	Spawn:
		TNT1 A 0;
		PICK A -1;
		Stop;
	}
}

class ThrownScythe : Actor
{	

	override void Tick()
	{
		Super.Tick();
		
		if (BounceCount == 1)
		{
			A_SpawnItemEx("PostScythe");
			Destroy();
		}
	
	}
	
	Default
	{
		Scale 2.5;
		Speed 7;
		+BOUNCEONWALLS
		+BOUNCEONFLOORS
		+BOUNCEONCEILINGS
		BounceCount 2;
		BounceSound "weapons/scythe_hitwall";
		SeeSound "";
		DeathSound "weapons/scythe_hitground";
		Projectile;
		+RANDOMIZE
		+INTERPOLATEANGLES
		+RIPPER
		Scale 1.2;
	}
  States
  {
  Spawn:
    THRO AA 6 Bright
	{
		A_PlaySound("weapons/scythe_throwloop", CHAN_WEAPON);
		
		Actor Woosh = SpawnPlayerMissile("Postal_Woosh");
		if (Woosh)
		{
			let pp = Postal_Woosh(Woosh);
			if (pp)
			{
				pp.Entity = self;
				pp.Scale.X = 2.5;
				pp.Scale.Y = 2.5;
			}
			
		}
	}
	Loop;
  Death:
	TNT1 A 0 A_SpawnItemEx("PostScythe");
    Stop;
  }
}