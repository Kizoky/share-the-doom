class PostRifle : PostalWeapon
{
	bool SniperSight;
	Default
	{
		//$Category STD Weapons
		//$Title "Sniper Rifle"
		//$Sprite PICKA0
		Weapon.Kickback 100;
		Weapon.SelectionOrder 1300;
		Weapon.AmmoUse 1;
		Weapon.AmmoGive 8;
		Weapon.AmmoType "Clip";
		Weapon.BobRangeX 0.4;
		Weapon.BobRangeY 0.5;
		Obituary "$PSTL_OB_RIFLE";
		Tag "$PSTL_WP_RIFLE";
		Inventory.PickupMessage "$PSTL_PP_RIFLE";
		PostalWeapon.STD_Gun true;
	}
	States
	{	
	Ready:
		RIFI ABCDEFGHIJKLMNOPQRST 4
		{	
			let s = player.findPSprite(PSP_Weapon);
			if (s) 
			{
				if (invoker.SniperSight) 
					return ResolveState("Ready.Zoom");	
				else if (Countinv("Clip") < 1)
					s.frame = 3;
			}
			
			A_WeaponReady(true);
				
			return ResolveState(null);
		}
		Loop;
	Ready.Zoom:
		RIF2 A 1
		{
			if (player.cmd.buttons & BT_ATTACK)
				return ResolveState("FireZoomed");
				
			if (player.cmd.buttons & BT_ALTATTACK)
				return ResolveState("NoAim");
			
			return ResolveState(null);
		}
		Loop;				
	Deselect:
		RIFD ABCDEFGHIJKLMNOPQRSTUVWXY 1;
		RIFD Y 0 A_PostalLower();
		Loop;
	Select:
		RIFS A 0 A_PostalRaise(0);
		RIFS A 0 A_PlaySound("weaponselect4", 7 | 4096);
		RIFS ABCDEFGHIJKLMNOPQRS 2 A_WeaponReady;
		RIFI A 0 A_PostalRaise(1);
		Loop;
	Fire:
		TNT1 A 0
		{
			if (CountInv("Clip") < 1)
			
			if (invoker.SniperSight)
				return ResolveState("FireZoomed");
			
			A_PlaySound("weapons/snifire");;
			
			return ResolveState(null);
		}	
		RIFF A 0 BRIGHT A_FireBullets (1, 1, 1, 20);
		RIFF ABCDEF 3;
		TNT1 A 0 A_PlaySound("weapons/snibolt", 1 );
		RIFF GHIJKLMNOPQRSTUVW 2;
		Goto Ready;
	FireZoomed:
		RIF2 A 0 
		{
			if (CountInv("Clip") < 1)
				return resolveState("NoAim");
			
			self.A_TakeInventory("Clip",1);
			
			let plr = PlayerPawn(self);
			if (plr)
				plr.AttackZOffset = 13;
			
			A_PlaySound("weapons/snifire", 1 );
			A_SetPitch(pitch - 0.3,SPF_INTERPOLATE);
			
			if (plr)
				plr.AttackZOffset = 8;
			
			return resolveState(null);
		}
		RIF2 A 0 BRIGHT A_FireBullets (1, 1, 1, 20);
		RIF2 AAAAAA 3;
		RIF2 A 0 A_PlaySound("weapons/snibolt", 1 );
		RIF2 AAAAAAAAAAAAAAAAA 2;
		Goto Ready.Zoom;
	AltFire:
		RIFZ A 0
		{
			if (invoker.SniperSight)
			return ResolveState ("NoAim");
			
			invoker.SniperSight = true; 			
			A_ZoomFactor(2.0);
			
			return ResolveState (null);
		}
		RIFZ ABCDEFGHIJ 1;
		Goto Ready.Zoom;
    NoAim:
		RIFZ J 0 
		{ 
			invoker.SniperSight = false;
			A_ZoomFactor(1.0);
		}	   
        RIFZ JIHGFEDCBA 1;
        Goto Ready;
	Flash:
		//PGNS B 3 Offset(0, 0) Bright A_Light2;
		//PGNS A 2 Offset(0, 0) Bright A_Light1;
		Goto LightDone;
	Spawn:
		TNT1 A 0;
		PICK A -1;
		Stop;
	}
}