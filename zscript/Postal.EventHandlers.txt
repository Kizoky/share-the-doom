class DudeCoreHandler : EventHandler 
{
	
	int SavedTimes;
	bool SavedTrigger;
	
   override void NetworkProcess (ConsoleEvent e)
   {
	  //let vars = PostalVariables(GlobalsContainer.Find("PostalVariables"));
      if (e.Name ~== "Kicking")
	  {
			PostalDude p = PostalDude(players[e.Player].mo);
			PlayerInfo player = players [e.Player];
			
			if (p.bKILLED) return;
			if (p.SuicideCamera) return;
			
			//Console.Printf("Q pressed (Mighty Foot Engaged)");
			if (p)
				p.Kick = true;
	  }
	  
	  if (e.Name ~== "Holster")
	  {
			PlayerInfo player = players [e.Player];
			PostalDude p = PostalDude(players[e.Player].mo);
			
			if (p.bKILLED) return;
			if (p == null || p.PlayerData == null) return;
			if (p.SuicideCamera) return;
			
				if (p.PlayerData.Holster == false)
				{
					p.PlayerData.HolsterSave = p.player.ReadyWeapon;
					//Console.Printf("E is pressed (Holster is true)");
					p.Player.PendingWeapon = p.PlayerData.HolsterWeapon;
					p.PlayerData.Holster = true;
				}
				else
				{
					//Console.Printf("E is pressed (Holster is false)");
					if (p.Player.ReadyWeapon == p.PlayerData.HolsterWeapon)
					{
						p.player.PendingWeapon = p.PlayerData.HolsterSave;
						p.PlayerData.Holster = false;
					}
					else
					{
						p.PlayerData.HolsterSave = p.player.ReadyWeapon;
						p.PlayerData.Holster = false;
					}
				}
	  }

	  if (e.Name ~== "Urethra")
	  {
			PlayerInfo player = players [e.Player];
			PostalDude p = PostalDude(players[e.Player].mo);
			//Console.Printf("R is pressed (Now the flowers will grow)");
			
			//Pretty much it works the same way like Holster
			
			if (p.bKILLED) return;
			if (p == null || p.PlayerData == null) return;
			if (p.SuicideCamera) return;
			
			if (p.PlayerData.Urethra == false)
			{
				if (p.Player.ReadyWeapon == p.PlayerData.UrethraWeapon)
				{
					p.PlayerData.Urethra = false;
					//Console.Printf("ReadyWeapon is same as UrethraWeapon");
				}
				else
				{
					p.PlayerData.UrethraSave = p.Player.ReadyWeapon;
					p.PlayerData.Urethra = true;
					p.Player.PendingWeapon = p.PlayerData.UrethraWeapon;
					//Console.Printf("ReadyWeapon is not same as UrethraWeapon");
				}
			}
			else
			{
				if (p.player.ReadyWeapon != p.PlayerData.UrethraSave)
				{
					p.Player.PendingWeapon = p.PlayerData.UrethraSave;
					p.PlayerData.Urethra = false;
					//Console.Printf("If ReadyWeapon not same as PD_UrethraSave");
				}
				else
				{
					p.PlayerData.Urethra = false;
					//Console.Printf("If ReadyWeapon not same as PD_UrethraSave (else)");
				}
			}
	  }
	  
	  if (e.Name ~== "Suicide")
	  {
			PostalDude p = PostalDude(players[e.Player].mo);
			PlayerInfo player = players [e.Player];
			
			if (p.bKILLED) return;
			
			//Console.Printf("K pressed (I regret nothing)");
			if (p == null || p.PlayerData == null) return;
			
			if (!(p.pos.z ~== p.floorz)) return;
			
			p.PlayerData.Suicide = true;
			p.SuicideAngle = p.Angle;
			
			
	  }
	  
	  if (e.Name ~== "Map")
	  {
			PostalDude p = PostalDude(players[e.Player].mo);
			PlayerInfo player = players [e.Player];
			
			if (p.bKILLED) return;
			if (p == null) return;
			if (p.SuicideCamera) return;
			
			Console.Printf("M pressed");
			if (p.Map == false)
			{
				p.Map = true;
				p.A_PlaySound("map/uncrumple",0);
			}
			else
			{
				p.Map = false;
				p.A_PlaySound("map/crumple",0);
			}
	  }
	  
	  if (e.Name ~== "DamageSelf")
	  {
			PostalDude p = PostalDude(players[e.Player].mo);
			PlayerInfo player = players [e.Player];
		
		if (p)
			p.A_DamageSelf(5);
		
	  }
	  
	  if (e.Name ~== "DropInventoryItem" || e.Name ~== "DropWeaponItem")
	  {
			PostalDude p = PostalDude(players[e.Player].mo);
			PlayerInfo player = players [e.Player];
			
			// Player drops currently selected inventory item
			// todo: Special inventory drops, for ex.: Catnip, pissed donuts, etc...
			
			if (p && e.Name ~== "DropInventoryItem")
			{
				if (p.SuicideCamera || p.isInventoryUsed) return;
				
				let item = p.invSel.GetClassName();
				p.A_TakeInventory(item,1);
				
				let itemspawn = p.Spawn(item, p.pos+(0,0,45));
				if (itemspawn)
				{
					itemspawn.angle = p.angle;
					itemspawn.A_Recoil(-4);
					itemspawn.vel.z += 0.5;
					itemspawn.angle = random(0,360);
				}
			}
			
			// Player drops currently equipped weapon
			
			if (p && e.Name ~== "DropWeaponItem")
			{
				if (p.SuicideCamera || p.isWeaponThrown) return;
				
				// Note: Player must absolutely not drop these 3 weapons
				if (p.player.ReadyWeapon == Weapon(p.FindInventory("PostHands")) ||
					p.player.ReadyWeapon == Weapon(p.FindInventory("PostUrethra")) ||
					p.player.ReadyWeapon == Weapon(p.FindInventory("PostMatchbox"))) return;
				
				// Check if weapon is in Ready state, if not do NOT drop the weapon
				// Note: This makes isWeaponThrown check totally useless
				let psp = p.player.getpsprite(1);
				let wep = p.player.ReadyWeapon;
				
				if (psp && wep && !(p.InStateSequence(psp.curstate, wep.ResolveState("Ready")))) return;
				
				// Get the weapon's name directly
				let item = wep.GetClassName();
				
				// Force the player to holster
				p.player.PendingWeapon = Weapon(p.FindInventory("PostHands"));
				
				// Take away the weapon
				p.A_TakeInventory(item);
				
				// Let's spawn a new weapon
				let itemspawn = p.Spawn(item, p.pos+(0,0,55));
				if (itemspawn)
				{
					let a = PostalWeapon(itemspawn);
					if (a)
					{
						a.angle = p.angle;
						a.A_Recoil(-6);
						a.vel.z += 0.5;
						a.angle = random(0,360);
						a.STD_Dropped = true;
					}
				}
			}
	  }
	  
			// Zoom in or out Suiciding
			
			if (e.Name ~== "ZoomSuicide")
			{
				PostalDude p = PostalDude(players[e.Player].mo);
				PlayerInfo player = players [e.Player];
				
				if (p.SuicideCamera && p)
				{
					let a = CameraRunner(p.SuicideCamera);
					
					if (a)
					{
						if(e.Args[0] == 0x198)
						{
							if (a.zoomH + 2 >= 100) return;
							
							a.zoomH += 2;
						}
							
						if (e.Args[0] == 0x199)
						{
							if (a.zoomH - 2 <= -80) return;
							
							a.zoomH -= 2;
						}
					}
				}
			}
	  
   }
   
   override void PlayerEntered (PlayerEvent e)
   {
		PostalDude p = PostalDude(players[e.PlayerNumber].mo);
		
		if (p != null)
		{
			p.A_TakeInventory("PostKick",100);
			p.Kick = false;
			return;
		}
		
		PlayerPawn n = PlayerPawn(players[e.PlayerNumber].mo);
		if (n && p == null)
		{
			n.A_GiveInventory("HUDViewable",1);
			n.A_GiveInventory("YouSpinMyHeadRightRoundRightRoundLikeARecordBabyRightRoundRightRound",1);
		}
   }
   
   //Needed for fire and other things
   override void WorldThingSpawned (Worldevent e)
   {
		if (!(e.thing is 'PostalActor') && e.thing.bISMONSTER) 
		{
			e.thing.bSPECIAL = true;
			e.thing.bPICKUP = true;
		}
		
		CVar lightscvar = CVar.FindCVar('postal_lights');
		bool lights = (lightscvar != null && lightscvar.GetBool ());
		
		if (!lights)
		{
			if (e.thing is 'PointLight')
				e.thing.Destroy();
		}
		
		CVar grasscvar = CVar.FindCVar('postal_grass');
		bool grass = (grasscvar != null && grasscvar.GetBool ());
		
		if (!grass)
		{
			if (e.thing is 'grass1' || e.thing is 'grass2' || e.thing is 'weedS1' || e.thing is 'weedS2')
				e.thing.Destroy();
		}
		
		// Replacing ZDoom patrolpoints on STD levels with Carnodes
		bool STD_Levels =
		(
			Level.Mapname.Left(5) ~== "world" ||
			Level.Mapname ~== "DOOMTEST"
		);
		
		let SpawnedThing = e.Thing;
		
		if (SpawnedThing is "PatrolPoint" && STD_Levels)
		{
			actor nodee = SpawnedThing.Spawn("Postal_Carnode", SpawnedThing.pos);
			let node = Postal_Carnode(nodee);
			let ZDpatrolpoint = PatrolPoint(SpawnedThing);
			
			if (node && ZDpatrolpoint)
			{
				node.user_PatrolNextTag = ZDpatrolpoint.args[0];
				node.user_PatrolTag = ZDpatrolpoint.tid;
				node.angle = ZDpatrolpoint.angle;
				ZDpatrolpoint.Destroy();
			}
		}
		
		CVar monstacvar = CVar.FindCVar('postal_monsterreplacee');
		bool monsta = (monstacvar != null && monstacvar.GetBool ());
		
		if (!STD_Levels && monsta)
		{
			if (!SpawnedThing) return;
			
			if (SpawnedThing is 'BossBrain' ||
				SpawnedThing is 'BossEye' ||
				SpawnedThing is 'BossTarget' ||
				SpawnedThing is 'CommanderKeen' ||
				SpawnedThing is 'Sorcerer1' ||
				SpawnedThing is 'Sorcerer2' ||
				SpawnedThing is 'Dragon' ||
				SpawnedThing is 'ClericBoss' ||
				SpawnedThing is 'MageBoss' ||
				SpawnedThing is 'FighterBoss' ||
				SpawnedThing is 'Heresiarch' ||
				SpawnedThing is 'Korax' ||
				SpawnedThing is 'StrifeHumanoid' ||
				SpawnedThing is 'SpectralMonster' ||
				SpawnedThing is 'EntityNest' ||
				SpawnedThing is 'EntityPod' ||
				SpawnedThing is 'Loremaster' ||
				SpawnedThing is 'Oracle' ||
				SpawnedThing is 'Programmer' ||
				SpawnedThing is 'StrifeBishop' ||
				SpawnedThing is 'KneelingGuy')
					return;
			
				if (SpawnedThing.bISMONSTER && SpawnedThing.Health > 0 && SpawnedThing)
				{
					int chance = random[cc](0,50);
					if (chance >= 25 && SpawnedThing.Tid == 0)
					{
						actor NPC = SpawnedThing.Spawn("NPCCore", SpawnedThing.pos);
						if (NPC)
						{
							NPC.angle = SpawnedThing.angle;
							
							/*
							NPC.ChangeTid(SpawnedThing.tid);
							
							int j = 0;
							while (j < 5)
							{
								NPC.args[j] = SpawnedThing.args[j];
								j++;
							}
							*/
							
							SpawnedThing.Destroy();
						}
					}
					else SpawnedThing.A_GiveInventory("PostalNPCMonsterChecker");
				}
		}
		
   }
	
	// Purely only used for mocking the player for cheating
	// Totally harmless!
	override void WorldTick()
	{
		if (gameaction == ga_savegame || gameaction == ga_autosave)
		{
			let player = players[consoleplayer].mo;
			
			if (SavedTimes == 4 && SavedTrigger != true)
			{
				player.A_PlaySound("dude_save2",0);
				SavedTrigger = true;
			}
			
			if (SavedTimes == 5 && SavedTrigger != true)
			{
				player.A_PlaySound("dude_save1",0);
				SavedTrigger = true;
			}
			
			if (SavedTimes == 6 && SavedTrigger != true)
			{
				player.A_PlaySound("dude_save3",0);
				SavedTimes = 0;
				SavedTrigger = true;
			}
			
			SavedTimes++;
			SavedTrigger = false;
		}
	
	}
	
	// Used for initiating Dude's killing spree lines
	// This is universal
	override void WorldThingDied(WorldEvent e)
	{	
		if (e.Inflictor && e.Thing)
		{
			let player = players[consoleplayer].mo;
			let p = PostalDude(player);
			
			if (!p) return;
			
			if (e.Thing != p)
			{
				//Is there a way to maybe get the killer and not the target?
				if (e.Thing.target == p)
				{
					if (p != null && !(p.bKILLED))
					{
						let pa = PostalActor(e.Thing);
						if (pa && pa.STD_NoKillLine) return;
						
						p.InitLine = true;
					}
				}
			}
			//else if (e.Thing == PostalDude(e.Inflictor))
			//	Console.Printf("Player suicided!");
		}
	}
	
	override bool InputProcess (InputEvent e)
	{
		if (e.KeyScan == e.Key_MWheelUp || e.KeyScan == e.Key_MWheelDown)
			SendNetworkEvent("ZoomSuicide", e.KeyScan);
			
		return false;
	}
}
